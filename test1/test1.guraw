#!/usr/bin/env gura
import(wx)
import(resource)

App = class(wx.App) {
	OnInit() = {
		frame = FrameMain('Simple')
		frame.Show()
		true
	}
}

PanelMain = class(wx.Panel) {
	TimerId = 1
	__init__(parent:wx.Windowm) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
			ctrl.Bind(wx.EVT_TEXT) {|event| this.OnText(event)}
			this.textEnter = ctrl
		}
		wx.TextCtrl(this, wx.ID_ANY, style => wx.TE_MULTILINE) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand())
			this.textLog = ctrl
		}
		this.timer = wx.Timer(this, this.TimerId)
		this.Bind(wx.EVT_TIMER, this.TimerId) {|event| this.OnTimer(event)}
	}
	OnText(event:wx.CommandEvent) = {
		this.timer.Start(500, true)
	}
	OnTimer(event:wx.TimerEvent) = {
		YYYY = '2015'
		YY = '15'
		MM = '03'
		DD = '21'
		hh = '13'
		mm = '24'
		ss = '47'
		strTmpl = this.textEnter.GetValue()
		try {
			this.textLog.SetValue(strTmpl.embed())
		} catch {|e|
			this.textLog.SetValue(e.text)
		}
	}
}

FrameMain = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.DefaultPosition,
			size:wx.Size => wx.DefaultSize) = {|nil, wx.ID_ANY, title, pos, size|
		this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		menuBar = wx.MenuBar()
		PanelMain(this)
		this.SetMenuBar(menuBar)
	}
}

wx.IMPLEMENT_APP(App)

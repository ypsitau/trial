#!/usr/bin/env gura
import(wx)
import(jpeg)
import(resource)

App = class(wx.App) {
	OnInit() = {
		frame = MainFrame()
		frame.Show()
		true
	}
}

PatternEditorPanel = class(wx.Panel) {
	TimerId = 1
	YYYY = '2015'
	YY = '15'
	MM = '03'
	DD = '21'
	hh = '13'
	mm = '24'
	ss = '47'
	SEQ = 'AA'
	__init__(parent:wx.Windowm) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		str = '${YYYY}/${YYYY}${MM}${DD}/${YYYY}_${MM}${DD}_${hh}${mm}${ss}${SEQ}'
		wx.TextCtrl(this, wx.ID_ANY, str) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 4))
			ctrl.Bind(wx.EVT_TEXT) {|event| this.OnText(event)}
			this.textEnter = ctrl
		}
		wx.StaticText(this, wx.ID_ANY,
					  format('Sample: '$ + '%s-%s-%s %s:%s:%s', YYYY, MM, DD, hh, mm, ss)) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 4))
		}
		wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 4))
			this.textLog = ctrl
		}
		this.timer = wx.Timer(this, TimerId)
		this.Bind(wx.EVT_TIMER, this.TimerId) {|event| this.OnTimer(event)}
		this.UpdateSample()
	}
	OnText(event:wx.CommandEvent) = {
		this.timer.Start(500, true)
	}
	OnTimer(event:wx.TimerEvent) = {
		this.UpdateSample()
	}
	UpdateSample() = {
		strTmpl = this.textEnter.GetValue()
		try {
			str = strTmpl.embed()
			this.textLog.SetForegroundColour(wx.BLACK)
			this.textLog.SetValue(str)
		} catch {|e|
			this.textLog.SetForegroundColour(wx.RED)
			this.textLog.SetValue(e.text)
		}
	}
}

PatternEditorFrame = class(wx.Frame) {
	__init__(pos:wx.Point => wx.DefaultPosition) = \
					{|nil, wx.ID_ANY, 'Pattern Editor'$, pos, wx.Size(800, 300)|
		this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		menuBar = wx.MenuBar()
		PatternEditorPanel(this)
		this.SetMenuBar(menuBar)
	}
}

ContainerInfo = struct(dirName:string, y:number, rc:wx.Rect, expandFlag:boolean, images[]:image => [])

ImageViewer = class(wx.Panel) {
	__init__(parent:wx.Windowm, style:number) = \
						{|parent, style => (style | wx.HSCROLL | wx.VSCROLL)|
		this.bmpScreen = nil
		this.brushBg = wx.Brush(wx.WHITE, wx.BRUSHSTYLE_SOLID)
		this.brushSlot = wx.Brush(wx.Colour(200, 200, 200), wx.BRUSHSTYLE_SOLID)
		this.containerInfos = []
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_SCROLLWIN) {|event| this.OnScrollWin(event)}
		this.Bind(wx.EVT_MOUSEWHEEL) {|event| this.OnMouseWheel(event)}
		this.Bind(wx.EVT_LEFT_DOWN) {|event| this.OnLeftDown(event)}
		this.Bind(wx.EVT_LEFT_UP) {|event| this.OnLeftUp(event)}
		this.Bind(wx.EVT_MOTION) {|event| this.OnMotion(event)}
		this.UpdateContainerInfos()
	}
	UpdateContainerInfos() = {
		htLine = 20
		dirNameTop = '/Users/ypsitau/Pictures'
		dirNames = (path.walk(dirNameTop, 1, '*'):dir).sort():list
		this.containerInfos = dirNames.each():list {|dirName, idx|
			y = idx * htLine
			ContainerInfo(dirName, y, wx.Rect(0, y, 10, 20), false)
		}
	}
	UpdateContent() = {
		wdMgnLeft = 20
		wdSkip = 8, htSkip = 8
		htLine = 20
		sizeThumbnail = 150
		[wdClient, htClient] = this.GetClientSizeWH()
		y = 0
		this.containerInfos.each {|containerInfo|
			containerInfo.y = y
			containerInfo.rc.y = y
			y += htLine
			if (containerInfo.expandFlag) {
				if (containerInfo.images.isempty()) {
					pathNames = path.dir(containerInfo.dirName, '*.JPG')
					println(pathNames)
					containerInfo.images = image(pathNames)::thumbnail(sizeThumbnail):box
				}
				nRows = int((containerInfo.images.len() + 5) / 6)
				htImages = nRows * sizeThumbnail
				if (nRows > 1) { htImages += (nRows - 1) * htSkip }
				y += htImages
			}
		}
		htRequired = y
		htScreen = max(htClient, htRequired)
		this.bmpScreen = wx.BitmapWH(wdClient, htScreen)
		wx.MemoryDC(this.bmpScreen) {|dc|
			dc.SetBackground(this.brushBg)
			dc.Clear()
			dc.SetPen(wx.NullPen)
			dc.SetBrush(this.brushSlot)
			this.containerInfos.each {|containerInfo|
				y = containerInfo.y
				images = containerInfo.images
				dc.DrawText(cond(containerInfo.expandFlag, '-', '+'), 0, y)
				dc.DrawText(containerInfo.dirName, 20, y)
				if (containerInfo.expandFlag) {
					xsSlot = wdMgnLeft + (range(images.len()) % 6) * (sizeThumbnail + wdSkip)
					ysSlot = y + htLine + int(range(images.len()) / 6) * (sizeThumbnail + htSkip)
					xsImage = xsSlot + (sizeThumbnail - images:*width) / 2
					ysImage = ysSlot + (sizeThumbnail - images:*height) / 2
					dc.DrawRectangle(xsSlot, ysSlot, sizeThumbnail, sizeThumbnail)
					dc.DrawBitmap(images, xsImage, ysImage, false)
				}
			}
		}
		y = 0
		this.SetScrollbar(wx.VERTICAL, y, htClient, htScreen)
		this.Refresh()
		this.Update()
	}
	SenseBox(pt:wx.Point, clickFlag:boolean) = {
		idx = this.containerInfos:*rc:*Contains(pt).find():index
		if (idx) {
			if (clickFlag) {
				containerInfo = this.containerInfos[idx]
				containerInfo.expandFlag = !containerInfo.expandFlag
			}
			`expandButton
		} else {
			`none
		}
	}
	OnLeftDown(event:wx.MouseEvent) = {
		rtn = this.SenseBox(event.GetPosition(), true)
		this.UpdateContent()
	}
	OnLeftUp(event:wx.MouseEvent) = {
	}
	OnMotion(event:wx.MouseEvent) = {
		rtn = this.SenseBox(event.GetPosition(), false)
		if (rtn == `expandButton) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_HAND))
		} else {
			this.SetCursor(wx.StockCursor(wx.CURSOR_ARROW))
		}
	}
	OnSize(event:wx.SizeEvent) = {
		this.UpdateContent()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		if (this.bmpScreen) {
			x = this.GetScrollPos(wx.HORIZONTAL)
			y = this.GetScrollPos(wx.VERTICAL)
			dc.DrawBitmap(this.bmpScreen, -x, -y, false)
		}
		dc = nil
	}
	OnScrollWin(event:wx.ScrollWinEvent) = {
		eventType = event.GetEventType()
		orientation = event.GetOrientation()
		if (eventType == wx.EVT_SCROLLWIN_TOP.GetEventType()) {
			pos = 0
		} elsif (eventType == wx.EVT_SCROLLWIN_BOTTOM.GetEventType()) {
			pos = this.GetScrollRange(orientation)
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 10
		} elsif (eventType == wx.EVT_SCROLLWIN_LINEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 10
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEUP.GetEventType()) {
			pos = this.GetScrollPos(orientation) - 100
		} elsif (eventType == wx.EVT_SCROLLWIN_PAGEDOWN.GetEventType()) {
			pos = this.GetScrollPos(orientation) + 100
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBTRACK.GetEventType()) {
			pos = event.GetPosition()
		} elsif (eventType == wx.EVT_SCROLLWIN_THUMBRELEASE.GetEventType()) {
			pos = event.GetPosition()
		}
		this.SetScrollPos(orientation, pos)
		this.Refresh()
	}
	OnMouseWheel(event:wx.MouseEvent) = {
		orientation = wx.VERTICAL
		rot = event.GetWheelRotation()
		pos = this.GetScrollPos(orientation)
		if (rot < 0) {
			this.SetScrollPos(orientation, pos + 10)
			this.Refresh()
		} elsif (rot > 0) {
			this.SetScrollPos(orientation, pos - 10)
			this.Refresh()
		}
	}
}

MainPanel = class(wx.Panel) {
	__init__(parent:wx.Windowm) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand().Border(wx.ALL, 2))
		wx.FlexGridSizer(nil, 3, 4, 4) {|gbox|
			gbox.AddGrowableCol(1, 1)
			vbox.Add(gbox, wx.SizerFlags().Expand())
			wx.StaticText(this, wx.ID_ANY, 'Source'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
			}
			wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.Button(this, wx.ID_ANY, 'Browse'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
			}
			wx.StaticText(this, wx.ID_ANY, 'Destination'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
			}
			wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.Button(this, wx.ID_ANY, 'Browse'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
			}
			wx.StaticText(this, wx.ID_ANY, 'Pattern'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
			}
			wx.StaticText(this, wx.ID_ANY, '', style => wx.SUNKEN_BORDER) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.Button(this, wx.ID_ANY, 'Edit'$) {|ctrl|
				gbox.Add(ctrl, wx.SizerFlags())
			}
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Expand().Border(wx.TOP, 4))
			wx.Button(this, wx.ID_ANY, 'Run'$) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags())
			}
			wx.Gauge(this, wx.ID_ANY, 100, style => wx.GA_HORIZONTAL) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.LEFT, 4))
			}
		}
		ImageViewer(this, style => wx.SUNKEN_BORDER) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.TOP, 4))
		}
	}
}

MainFrame = class(wx.Frame) {
	__init__(pos:wx.Point => wx.DefaultPosition) = \
					{|nil, wx.ID_ANY, 'Getter Photo', pos, wx.Size(800, 600)|
		this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		menuBar = wx.MenuBar()
		MainPanel(this)
		this.SetMenuBar(menuBar)
	}
}


wx.IMPLEMENT_APP(App)
